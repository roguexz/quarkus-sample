buildscript {
    repositories {
        // If we are testing locally built Quarkus, then use mavenLocal()
        if (VERSION_QUARKUS == '999-SNAPSHOT') {
            mavenLocal()
        }
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath("io.quarkus:gradle-application-plugin:$VERSION_QUARKUS")
    }
}

repositories {
    repositories {
        // If we are testing locally built Quarkus, then use mavenLocal()
        if (VERSION_QUARKUS == '999-SNAPSHOT') {
            mavenLocal()
        }
        mavenCentral()
    }
}

///
/// Group details
///
group = 'io.rogue.quarkus-test'


// ----------------------------------------------------
//
// Configure the common settings for all projects.
//
// ----------------------------------------------------

buildDir = file(rootDir.path + File.separator + 'tmp' + File.separator + rootProject.relativePath(projectDir))

apply plugin: 'java'
apply plugin: 'io.quarkus'

dependencies {

    // Enforce the Quarkus BOM for various dependencies.
    implementation platform("io.quarkus:quarkus-bom:$VERSION_QUARKUS") {
        if (VERSION_QUARKUS.endsWith("-SNAPSHOT")) {
            changing true
        }
    }

    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-resteasy-jackson'
    implementation 'io.quarkus:quarkus-smallrye-openapi'

    testImplementation 'io.quarkus:quarkus-junit5'
}

compileJava {
    options.compilerArgs << '-parameters'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}


test {
    // Show standard streams on the console.
    testLogging {
        showStandardStreams = true
    }
    // VPN issues at my place
    systemProperty 'quarkus.http.host', 'localhost'
}
